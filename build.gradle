apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'application'

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    version = stdout.toString().trim().split("-")
    return version[0..-2].join(".")
}

buildscript {
    ext.kotlin_version = '1.0.1-2'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
apply plugin: 'kotlin'

mainClassName = "theplace.ThePlaceApplication"

repositories {
    mavenCentral()
}

dependencies {
    compile "com.mashape.unirest:unirest-java:1.4.7"
    compile "org.jsoup:jsoup:1.8.3"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'no.tornado:tornadofx:1.4.1'
    compile 'commons-io:commons-io:2.4'
    compile 'com.google.code.gson:gson:2.6.2'
}

jar {
    manifest {
        attributes 'Main-Class': 'theplace.ThePlaceApplication',
                   'Application-Name': 'Celebs-Image-Viewer',
                   'Implementation-Version': getVersionName()
    }
    version getVersionName()
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

uploadArchives {
    repositories {
        flatDir {
            dirs 'repos'
        }
    }
}

defaultTasks 'run'